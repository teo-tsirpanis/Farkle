"Name" = 'JSON'
"Author" = 'Theodore Tsirpanis'
"Version" = '1.0.0'
"About" = 'A language describing the JavaScript Object Notation'
"Case Sensitive" = true

"Start Symbol" = <Value>

{Number19} = [123456789]
{Hex Digit} = [1234567890ABCDEF]

{String Characters} = {All Valid} - ["\]

Whitespace = ({Space}|{LF}|{CR}|{HT})+

Number = '-'? ('0'|{Number19}{Number}*) ('.'{Number}+)? ([eE][+-]?{Number}+)?

String = '"' ({String Characters} | ('\' (["\/bfnrt] | 'u'{Hex Digit}{Hex Digit}{Hex Digit}{Hex Digit})))* '"'

<Value> ::= String
         |  Number
         |  <Object>
         |  <Array>
         |  'true'
         |  'false'
         |  'null'

<Array> ::= '[' <Array Optional> ']'

<Array Optional> ::= <Array Reversed>
                  |  <>

<Array Reversed> ::= <Array Reversed> ',' <Value>
                 |  <Value>

<Object> ::= '{' <Object Optional> '}'

<Object Optional> ::= <Object Element>
                   |  <>

<Object Element> ::= <Object Element> ',' String ':' <Value>
                  |  String ':' <Value>
